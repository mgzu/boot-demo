import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version "${springBootVersion}" apply false
    id 'java'
}

allprojects {
    group = 'com.example'
    version = '0.0.1-SNAPSHOT'

    repositories {
        maven {
            url 'https://repo.spring.io/milestone'
        }
        mavenCentral()
    }
}

ext {
    hutoolVersion = '5.8.8'
    mapstructVersion = '1.5.3.Final'
    bizlogSdkVersion = '3.0.1'
    easyRulesVersion = '4.1.0'
    // docs
    openapiVersion = '1.6.11'
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    repositories {
        maven {
            url 'https://repo.spring.io/milestone'
        }
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    dependencies {
        implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
        implementation 'org.jetbrains.kotlin:kotlin-reflect'

        // tool
        // https://mvnrepository.com/artifact/cn.hutool/hutool-core
        implementation "cn.hutool:hutool-core:${hutoolVersion}"
        // https://mvnrepository.com/artifact/io.github.mouzt/bizlog-sdk
        implementation "io.github.mouzt:bizlog-sdk:${bizlogSdkVersion}"
        // https://mvnrepository.com/artifact/org.jeasy/easy-rules-core
        implementation "org.jeasy:easy-rules-core:${easyRulesVersion}"
        implementation "org.jeasy:easy-rules-spel:${easyRulesVersion}"

        // dev
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly "org.mapstruct:mapstruct:${mapstructVersion}"
        annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

        // test
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17

        toolchain {
            vendor = JvmVendorSpec.BELLSOFT
        }
    }

    test {
        useJUnitPlatform()
    }

}

import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
	id 'org.springframework.boot' version "${springBootVersion}" apply false
//	id 'org.graalvm.buildtools.native' version "${nativeBuildToolsVersion}" apply false
	id 'org.jetbrains.kotlin.jvm' version "${kotlinVersion}" apply false
	id 'me.champeau.jmh' version "0.7.1" apply false
}

allprojects {
	group = 'com.github.mgzu'
	version = '0.0.1-SNAPSHOT'

	repositories {
		maven {
			url 'https://maven.aliyun.com/repository/public/'
		}
		maven {
			url 'https://maven.aliyun.com/repository/spring/'
		}
		mavenLocal()
		mavenCentral()
	}
}

ext {
	// https://mvnrepository.com/artifact/org.dromara.hutool/hutool-core
	hutoolVersion = '6.0.0-M10'
	// https://mvnrepository.com/artifact/org.mapstruct/mapstruct
	mapstructVersion = '1.5.5.Final'
	// https://mvnrepository.com/artifact/io.github.mouzt/bizlog-sdk
	bizlogSdkVersion = '3.0.6'
	// https://mvnrepository.com/artifact/org.jeasy/easy-rules-core
	easyRulesVersion = '4.1.0'
	// https://mvnrepository.com/artifact/com.google.guava/guava
	guavaVersion = '33.0.0-jre'
	// https://mvnrepository.com/artifact/org.jetbrains/annotations
	jetbrainsAnnotationsVersion = '24.1.0'
	// https://mvnrepository.com/artifact/cn.dev33/sa-token-spring-boot-starter
	saTokenVersion = '1.37.0'

	// docs
	// https://mvnrepository.com/artifact/org.springdoc/springdoc-openapi-starter-webmvc-ui
	openapiVersion = '2.3.0'

	// test
	// https://mvnrepository.com/artifact/org.openjdk.jmh/jmh-core
	jmhVersion = '1.37'
}

ignoreNoSourceProject = ext.ignoreNoSourceProject.split(",")

subprojects {
	if (ignoreNoSourceProject.contains(project.name)) {
		return
	}
	def applyMavenPublish = !project.name.startsWith(ext.bizProjectPrefix)

	apply plugin: 'io.spring.dependency-management'
	apply plugin: 'java'
	if (applyMavenPublish) {
		apply plugin: 'maven-publish'
	}

	repositories {
		maven {
			url 'https://maven.aliyun.com/repository/public/'
		}
		maven {
			url 'https://maven.aliyun.com/repository/spring/'
		}
		mavenLocal()
		mavenCentral()
	}

	dependencyManagement {
		imports {
			mavenBom SpringBootPlugin.BOM_COORDINATES
		}
	}

	dependencies {
		// tool
		implementation "org.dromara.hutool:hutool-core:${hutoolVersion}"
		implementation "com.google.guava:guava:${guavaVersion}"
		implementation "org.mapstruct:mapstruct:${mapstructVersion}"
		implementation 'org.hibernate.validator:hibernate-validator'

		// dev
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'
		annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
		compileOnly "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"

		// test
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testImplementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
		testCompileOnly 'org.projectlombok:lombok'
		testAnnotationProcessor 'org.projectlombok:lombok'
		testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

		// kotlin
		testImplementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	test {
		useJUnitPlatform()
	}

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21

	jar {
		duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	}

	if (applyMavenPublish) {
		def localProperties = project.rootProject.file('local.properties')
		def ghUsername = null
		def ghPassword = null
		if (localProperties.exists()) {
			Properties properties = new Properties()
			properties.load(localProperties.newDataInputStream())
			ghUsername = properties.getProperty("GITHUB_USERNAME")
			ghPassword = properties.getProperty("GITHUB_TOKEN")
		} else {
			ghUsername = System.getenv("GITHUB_USERNAME")
			ghPassword = System.getenv("GITHUB_TOKEN")
		}
		if (ghUsername == null || ghUsername.isEmpty()) {
			return
		}
		publishing {
			repositories {
				maven {
					name = "GitHubPackages"
					url = uri("https://maven.pkg.github.com/mgzu/boot-demo")
					credentials {
						username = ghUsername
						password = ghPassword
					}
				}
			}
			publications {
				gpr(MavenPublication) {
					from(components.java)
				}
			}
		}
	}

}
